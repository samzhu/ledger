plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.8'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.graalvm.buildtools.native' version '0.10.6'
	id 'org.cyclonedx.bom' version '2.3.0'
	id 'com.gorylenko.gradle-git-properties' version '2.5.4'
}

group = 'io.github.samzhu'
version = '0.0.1-SNAPSHOT'
description = 'LLM 用量帳本服務'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudGcpVersion', "7.4.1")
	set('springCloudVersion', "2025.0.0")
}

dependencies {
	
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	// implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-json'
	// T-Digest for latency percentile calculation (P50/P90/P95/P99)
	implementation 'com.tdunning:t-digest:3.3'
	// Observability
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-tracing-bridge-otel'
	runtimeOnly 'io.micrometer:micrometer-registry-otlp'
	// Event Driven
	implementation('org.springframework.cloud:spring-cloud-stream') {
		// Exclude Joda-Time Jackson module - not needed (using java.time)
		// and causes Native Image issues with missing timezone resources
		exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-joda'
	}
	// 生產環境使用 GCP Pub/Sub
	implementation "com.google.cloud:spring-cloud-gcp-pubsub-stream-binder"
	// GCP Secret Manager 整合 (sm@ 語法)
	implementation "com.google.cloud:spring-cloud-gcp-starter-secretmanager"
	// 開發環境使用 RabbitMQ
	implementation 'org.springframework.cloud:spring-cloud-stream-binder-rabbit'
	//
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.springframework.cloud:spring-cloud-stream-test-binder'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:mongodb'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "com.google.cloud:spring-cloud-gcp-dependencies:${springCloudGcpVersion}"
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

// Git Properties 配置 - 正確識別 tags
gitProperties {
	customProperty 'git.closest.tag.name', {
		// CI 環境：優先使用 GitHub Actions 的 GITHUB_REF_NAME
		def refName = System.getenv('GITHUB_REF_NAME')
		if (refName && refName =~ /^\d+\.\d+\.\d+.*/) {
			return refName
		}
		// 本地環境：使用 git describe
		try {
			return git.describe(tags: true, always: false) ?: ''
		} catch (Exception e) {
			return ''
		}
	}
}
